*****************************************************************
******STMicroelectronics MOSFET, IGBT and Bipolar Library *******
*****************************************************************
*																*
* Models provided by STMicroelectronics are not guaranteed to	*
* fully represent all the specifications and operating			*
* characteristics of the product behavior that they reproduce.	*
* The model describes the characteristics of a typical device.	*
* In all cases, the current product data sheet contains all		*
* information to be used like final design guidelines and the	*
* only actual performance specification.						*
* Altough models can be a useful tool in evaluating device		*
* performance, they cannot model exact device performance under	*
* all conditions.												*
* STMicroelectronics therefore does not assume any				*
* responsibility arising from their use.						*
* STMicroelectronics reserves the right to change models		*
* without prior notice.											*
*																*
* Note: The model doesn't take into account the drain, gate,	*
* source inductances.If these contributions have to be			*
* considered it is possible include the inductors externally.	*
* For this package the values can be estimated as follow		*
*																*
* Ldrain= 1nH ,Lsource=2nH and Lgate=2.5nH						*
*																*
* Rev 1.0 - Apr 2016		      								*
*****************************************************************

*****************************************************************
.SUBCKT STGD4M65DF2-V2 C G E

***********************************************************
E1    Tj     val_T      VALUE={TEMP}
R1   val_T   0      1m


.param Areacap={1} Area={1} GFS={1} Areamiller={1}

***********************************************************
Rtk Tj 0 1e010
Rtk1 Tj 0 1e010
***********************************************************
********************************DIODO AK ******************

V_bjt C 1z 0
G_anodo1 1z a1  VALUE={I_diode(V(1z,a1),v(Tj))}
r_esc 1z a1 500

***********************************GRdiode*************************************************

V2xx  a1 22xxx 0
E2xxx 22xxx 1y VALUE={R2(I(V2xx),V(TJ))}
.FUNC R2(I1,T2) {I1*(rdrain+rpa*((I1**a))**Rx)/((b*((T2+273)/300)**(rdtemp))*Area)}


.FUNC I_diode(z3,k3) {(exp(min(-15+z3/(did(k3)),7))+0)}
.FUNC did(k4) {0.065-50E-06*k4}

************************************************************
************************* Itail_1 **************************

.param soft=100  H2temp=5

R_edep E d_dedep {soft}
E_Eds d_dedep edep VALUE {(-V(aa,0))*1}
C_Cds edep E 100E-12
E_E001 ba 0 VALUE {(-1*((V(Tj))**(1))*I(V_BJT)*(H2temp))}
R_coda aa ba 1
C-coda aa 0 10p
***** itail********

Gtail d s value {(1/soft)*(IF(v(E,d_dedep)>0,v(E,d_dedep),0))}

****************************************************************
V_Read 1y d1 0

VLs E ss 0

VLG G g2 0

rgate g2 gat 4

*********************** I step **************************** 
.PARAM  Vth0=6.4 KpSat0=1  KpLin0=2  Rx=1   rpa=0
**********************************************************
.PARAM  rs=40m drs=-1.5 rdrain={40m/Area} rdtemp=-3
.PARAM  Vthx=4.5m KSat=-1.3   KLin=-1
.PARAM  lambda=0.001    
.PARAM  a=1 b=1   
***********************************************************
.FUNC vth1(x) {vth0-vthx*(x-27)}
.FUNC kpsat(x) {kpsat0*((x+273)/300)**(ksat)}
.FUNC kplin(x) {kplin0*((x+273)/300)**(klin)}

***********************************************************

Vdrain d1 d 0

****************************** GRS *****************************
r_grs ss s 1k
V1xx    11xxx ss 0
E1xxx 11xxx s  VALUE={R1(LIMIT(I(V1xx),-1MEG,1MEG),V(TJ))}
.FUNC R1(I1,T1)   {-(I1)*((rs/(Area*(((T1+273)/300)**(drs)))))}

******* gmos ******

Gmos d s value {GFS*(IF(V(d,s)>0,(IF(v(gat,s)<vth1(V(TJ)),0,
+(IF((V(d,s)<(v(gat,s)-vth1(V(TJ)))*kpsat(v(TJ))/kplin(v(TJ))),(1+LAMBDA*v(d,s))*kplin(v(TJ))/2*v(d,s)*
+(2*(v(gat,s)-vth1(V(TJ)))-kplin(v(TJ))/kpsat(v(TJ))*v(d,s)),
+(1+LAMBDA*v(d,s))*kpsat(v(TJ))/2*(v(gat,s)-vth1(V(TJ)))**2)))),0))}


************************* Capacitance *************************

**** CGS *******
Cref0 400 0 1E-12
E20 400 500 value {V(E,gat)}
V20 500 0 0
Gcdg0 gat E value {1.0*V(alfa0)*i(V20)*1E12*Areacap}
Rdummy gat E 10meg
Rcap0 0 alfa0 1E03
Ecap0 alfa0 0 TABLE = {V(C,E)}
+(0.1,571.8p) 
+(0.2,521.2p) 
+(0.5,427.7p) 
+(1,394.1p) 
+(2,384.4p) 
+(3,380.9p) 
+(5,377.3p) 
+(7,375.3p) 
+(10,373.5p) 
+(15,371.4p) 
+(20,370.1p) 
+(25,369p) 
+(30,368.2p) 
+(40,367.1p) 
+(50,366.1p) 
+(100,363p) 
+(150,361.3p) 
+(200,360p) 
+(250,359p) 
+(300,358p) 
+(350,357.3p) 
+(400,356.6p) 
+(450,356.1p) 
+(500,355.6p) 
+(550,354.8p) 
+(600,354p) 
+(650,353.3p) 

***** CMiller******
Edev dev1 0 value {v(d,gat)}
c_dev1 dev1 dev2 1E-12
R_dev dev2 dev3 1E-06
V_miller dev3 0 0 
G_miller d gat  value ={1*Areamiller*v(alfa)*i(V_miller)*1E12}

****************************************************************
****************************************************************
Rcap alfa 0 1E03
Ecap alfa 0 TABLE ={V(C,G)}
+(0.1,570p) 
+(0.2,188.19p) 
+(0.5,77.4p) 
+(1,39.12p) 
+(2,26.7p) 
+(3,21.61p) 
+(5,16.79p) 
+(7,14.32p) 
+(10,12.12p) 
+(15,9.96p) 
+(20,8.68p) 
+(25,7.85p) 
+(30,7.22p) 
+(40,6.34p) 
+(50,5.76p) 
+(100,4.19p) 
+(150,3.5p) 
+(200,3.12p) 
+(250,2.83p) 
+(300,2.65p) 
+(350,2.52p) 
+(400,2.37p) 
+(450,2.38p) 
+(500,2.39p) 
+(550,2.39p) 
+(600,2.39p) 
+(650,2.39p) 

************************* COSS ****************************

Edevc dev1c 0 value {v(d,s)}
c_dev1c dev1c dev2c 1E-12
R_devc dev2c dev3c 1m
V_millerc dev3c 0 0 
G_coss C E  value ={1*v(alfa2)*i(V_millerc)*1E12}

***********************************************************
Rcap2 alfa2 0 1E03
Ecap2 alfa2 0 TABLE ={V(C,E)}
+(0.1,376.68p) 
+(0.2,299.81p) 
+(0.5,170.33p) 
+(1,113.61p) 
+(2,83.13p) 
+(3,68.94p) 
+(5,54.74p) 
+(7,46.79p) 
+(10,39.46p) 
+(15,32.22p) 
+(20,27.77p) 
+(25,24.83p) 
+(30,22.67p) 
+(40,19.76p) 
+(50,17.82p) 
+(100,12.89p) 
+(150,10.66p) 
+(200,9.41p) 
+(250,8.55p) 
+(300,7.89p) 
+(350,7.33p) 
+(400,6.81p) 
+(450,6.48p) 
+(500,6.26p) 
+(550,6.14p) 
+(600,6.05p) 
+(650,6p) 

********************
V_sense2  sx s  0

************************ BV *****************************

G_bvdss   d1bvdss1   ss  VALUE={I_BVDSS(V(d1bvdss1,ss),v(Tj))}
R_GBDSS d1bvdss1 0 1E10
.FUNC  I_BVDSS(z,k1) {(exp(min(-50+z/(bvd(k1)),7))-1n)}
.FUNC bvd(k) {17+0.0060*k}
R_bvdss d1 d1bvdss1 10

************************* VF ******************************

R_diodo C d1z {resdiodo}
V_diodo d1zd d1z 0
G_diode E d1zd VALUE={IF_diode(V(E,d1zd),v(Tj))}
R_x     E d1zd 1E9
.FUNC IF_diode(z3,k3) {(exp(min(-15+z3/(diodo(k3)),7))-1n)}
.FUNC diodo(k4) {0.085-140E-06*k4}
.PARAM resdiodo={150m}
***********************************************************

************************** Recovery diode **********************

E_E002 ba1 0 VALUE {(-I(V_diodo))}
R_R002 aa1 ba1 1k
C aa1 0 10E-12
.param irrm1=1.5e4
R_edep1 C d_dedep1 1E3
E_Eds1 d_dedep1 edep1 VALUE {-V(aa1,0)*irrm1*Area*V(gf)}
C_Cds1 edep1 E {0.5p}
*****
rdv d cff 1
cdv cff s 1000p

Rcapa gf 0 1E03
Ecapa gf 0 TABLE ={V(Tj)}
+(25,0.05)
+(175,0.15)

.ENDS STGD4M65DF2-V2





